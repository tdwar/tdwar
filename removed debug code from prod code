public with sharing class Excludefrompipeline implements Schedulable {
    public void execute(SchedulableContext sc) {
        Set<Id> stepIds = new Set<Id>();
        Set<Id> deploymentIds = new Set<Id>();
        Set<Id> promotionIds = new Set<Id>();
        Set<Id> userStoryIds = new Set<Id>();

        List<ContentDocument> contentDocuments = new List<ContentDocument>();
        Set<Id> cdIdstoDelete = new Set<Id>();
        Set<Id> cdIdstoDeleteWithTitle = new Set<Id>();

        if (Schema.sObjectType.copado__step__c.fields.createddate.isAccessible() &&
            Schema.sObjectType.copado__step__c.fields.copado__Status__c.isAccessible()) {

            List<copado__step__c> steps = [SELECT Id, copado__Deployment__c
                                           FROM copado__step__c
                                           WHERE copado__Deployment__r.createddate < LAST_N_DAYS: 120
                                           AND copado__Deployment__r.copado__Status__c IN ('Completed Successfully', 'Cancelled', 'Completed with Errors') 
                                           WITH SECURITY_ENFORCED];

            for (copado__step__c step : steps) {
                stepIds.add(step.Id);
                deploymentIds.add(step.copado__Deployment__c);
            }

            if (!stepIds.isEmpty()) {
                for (ContentDocumentLink cdLink : [SELECT Id, ContentDocumentId, LinkedEntityId 
                                                   FROM ContentDocumentLink 
                                                   WHERE LinkedEntityId IN :stepIds 
                                                   WITH SECURITY_ENFORCED]) {
                    cdIdstoDelete.add(cdLink.ContentDocumentId);
                }
            }
        }

        if (!cdIdstoDelete.isEmpty()) {
            contentDocuments = [SELECT Id, Title
                                FROM ContentDocument
                                WHERE Id IN :cdIdstoDelete
                                AND Title = 'Rollback metadata.zip' 
                                WITH SECURITY_ENFORCED];
        }

        for (ContentDocument cd : contentDocuments) {
            cdIdstoDeleteWithTitle.add(cd.Id);
        }

        if (!cdIdstoDeleteWithTitle.isEmpty()) {
            delete [SELECT Id FROM ContentDocument WHERE Id IN :cdIdstoDeleteWithTitle];
        }

        if (!deploymentIds.isEmpty()) {
            List<copado__Deployment__c> deploymentPromotions = [SELECT Id, copado__Promotion__c
                                                     FROM copado__Deployment__c
                                                     WHERE Id IN :deploymentIds 
                                                     WITH SECURITY_ENFORCED];

            for (copado__Deployment__c promotion : deploymentPromotions) {
                promotionIds.add(promotion.copado__Promotion__c);
            }
        }

        if (!promotionIds.isEmpty()) {
            List<copado__Promoted_User_Story__c> promotionUserStories = [SELECT Id, copado__Promotion__c, copado__User_Story__c
                                                       FROM copado__Promoted_User_Story__c
                                                       WHERE copado__Promotion__c IN :promotionIds 
                                                       WITH SECURITY_ENFORCED];

            for (copado__Promoted_User_Story__c userStory : promotionUserStories) {
                userStoryIds.add(userStory.copado__User_Story__c);
            }
        }

        if (!userStoryIds.isEmpty()) {
            List<copado__User_Story__c> userStoriesToUpdate = [SELECT Id, copado__Exclude_From_CBM__c, copado__Stop_Indexing_Metadata__c
                                                               FROM copado__User_Story__c
                                                               WHERE Id IN :userStoryIds 
                                                               WITH SECURITY_ENFORCED];

            for (copado__User_Story__c userStory : userStoriesToUpdate) {
                userStory.copado__Exclude_From_CBM__c = true;
                userStory.copado__Stop_Indexing_Metadata__c = true;
            }

            update userStoriesToUpdate;
        }
    }
}
